plugins {
    id 'java'
    id 'io.freefair.lombok' version '6.1.0'
    id 'idea'
}

group 'com.github.julian-mateu'
version '1.0'

repositories {
    mavenCentral()
}

sourceSets {
    integrationTest {
        compileClasspath += sourceSets.main.output
        runtimeClasspath += sourceSets.main.output
    }
}

idea {
    module {
        testSourceDirs += sourceSets.integrationTest.java.srcDirs
        testResourceDirs += sourceSets.integrationTest.resources.srcDirs
        scopes.TEST.plus += [ configurations.runtimeClasspath ]
    }
}

configurations {
    integrationTestImplementation.extendsFrom implementation
    integrationTestRuntimeOnly.extendsFrom runtimeOnly
}

dependencies {
    implementation 'org.apache.kafka:kafka-clients:2.8.0'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.12.5'
    implementation 'org.elasticsearch.client:elasticsearch-rest-high-level-client:7.14.0'
    implementation 'com.twitter:hbc-core:2.2.0'
    implementation 'io.github.cdimascio:dotenv-java:2.2.0'
    implementation 'org.slf4j:slf4j-log4j12:1.7.32'
    implementation 'com.evanlennick:retry4j:0.15.0'
    implementation 'com.google.inject:guice:5.0.1'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.2'
    testImplementation 'org.mockito:mockito-all:1.10.19'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.2'
    integrationTestImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.2'
    integrationTestRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.2'
}

test {
    useJUnitPlatform()
}

task integrationTest(type: Test) {
    description = 'Runs integration tests.'
    group = 'verification'

    useJUnitPlatform()
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
    shouldRunAfter test
}

check.dependsOn integrationTest
